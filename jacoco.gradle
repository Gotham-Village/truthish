import java.awt.*

// Jacoco code coverage
// See also: https://medium.com/@aldychris/kotlin-multiplatform-for-ios-and-android-mobile-application-96a753e175f7

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

project.afterEvaluate {
    // Create gradle task
    task jvmCoverage(type: JacocoReport, dependsOn: 'jvmTest') {
        group = "Reporting"
        description = "Generate Jacoco coverage reports on the common module build."

        def excludes = [
                '**/*Test*.*'
        ]

        classDirectories.setFrom(fileTree(
                dir: "${project.buildDir}/classes/kotlin/jvm/",
                excludes: excludes
        ))

        def coverageSourceDirs = [
                "src/commonMain/kotlin"
        ]

        additionalSourceDirs.setFrom(coverageSourceDirs)
        sourceDirectories.setFrom(coverageSourceDirs)
        executionData.setFrom(files("${project.buildDir}/jacoco/jvmTest.exec"))

        reports {
            xml.enabled = true
            html.enabled = true
        }

        doLast {
            Desktop.desktop.open file("${project.buildDir}/reports/jacoco/jvmCoverage/html/index.html")
        }
    }
}

